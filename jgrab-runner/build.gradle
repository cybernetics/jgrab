plugins {
    id 'java'
    id 'maven'
    id "com.jfrog.bintray" version "1.6"
}

sourceCompatibility = 1.8

description = 'Runs Java code without a build system, grabbing dependencies declared in the Java file itself.'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.ivy:ivy:2.4.0'
    compile 'com.athaydes.osgiaas:osgiaas-javac:0.7'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task fatJar( type: Jar ) {
    manifest {
        attributes 'Implementation-Title': 'JGrab',
                'Implementation-Version': version,
                'Main-Class': 'com.athaydes.jgrab.runner.JGrabRunner'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree( it ) } }
    with jar
}

test {
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
}

task localInstall( type: Copy, dependsOn: 'fatJar' ) {
    from fatJar, {
        rename '.*', 'jgrab.jar'
    }
    into "${System.getProperty( 'user.home' )}/.jgrab/"
}

/* Publishing config */

task javadocJar( type: Jar ) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar( type: Jar ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure( install.repositories.mavenInstaller ) {
    pom.project {
        inceptionYear '2017'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/jgrab'

        scm {
            connection 'git@github.com:renatoathaydes/jgrab.git'
            developerConnection 'git@github.com:renatoathaydes/jgrab.git'
            url 'https://github.com/renatoathaydes/jgrab'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

def getProjectProperty = { String propertyName ->
    project.properties[ propertyName ]
}

bintray {
    user = getProjectProperty "bintrayUserName"
    key = getProjectProperty "bintrayApiKey"
    configurations = [ 'archives' ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'jgrab'
        licenses = [ 'Apache-2.0' ]
        labels = [ 'java', 'rust', 'script-engine' ]
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = getProjectProperty 'ossrhUsername'
                password = getProjectProperty 'ossrhPassword'
                close = '1' // '0' to NOT close
            }
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar